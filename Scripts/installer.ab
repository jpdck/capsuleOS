// -----------------------------------------------//
// capsuleOS Installer                            //
// https://github.com/jpdck/capsuleOS             //
// -----------------------------------------------//

import * from "std/text"
import * from "std/env"
import * from "std/date"
import * from "std/fs"
import { file_download } from "std/http"
import * from "installer_utils.ab"

const RED = 31
const GREEN = 32
const YELLOW=33
const BLUE=34
const NC=0

const LOG_FILE = "installer.log"
const USER = trust env_var_get("USER")
const HOME = trust env_var_get("HOME")
const REPO_DIR = "{HOME}/Projects/capsuleOS"

let STAGE = 0

fun apply_macos_defaults(): Null? {
    silent {
        // Global Interface Settings
        $ defaults write NSGlobalDomain ApplePressAndHoldEnabled -bool false $?
        $ defaults write NSGlobalDomain AppleShowAllExtensions -bool true $?
        $ defaults write NSGlobalDomain InitialKeyRepeat -int 14 $?
        $ defaults write NSGlobalDomain KeyRepeat -int 1 $?
        $ defaults write NSGlobalDomain AppleInterfaceStyle -string "Dark" $?
        $ defaults write NSGlobalDomain AppleScrollerPagingBehavior -bool true $?
        $ defaults write NSGlobalDomain NSNavPanelExpandedStateForSaveMode -bool true $?
        $ defaults write NSGlobalDomain PMPrintingExpandedStateForPrint -bool true $?
        $ defaults write NSGlobalDomain "com.apple.trackpad.scaling" -float 2.0 $?
        $ defaults write NSGlobalDomain "com.apple.mouse.tapBehavior" -int 1 $?
        $ defaults write NSGlobalDomain AppleKeyboardUIMode -int 3 $?
        $ defaults write NSGlobalDomain com.apple.swipescrolldirection -bool true $?

        // Dock Configuration
        $ defaults write com.apple.dock autohide -bool false $?
        $ defaults write com.apple.dock orientation -string "bottom" $?
        $ defaults write com.apple.dock showhidden -bool true $?
        $ defaults write com.apple.dock mineffect -string "genie" $?
        $ defaults write com.apple.dock show-recents -bool false $?
        $ defaults write com.apple.dock tilesize -int 36 $?
        $ defaults write com.apple.dock magnification -bool true $?
        $ defaults write com.apple.dock largesize -int 48 $?
        $ defaults write com.apple.dock autohide-delay -float 0.0 $?
        $ defaults write com.apple.dock autohide-time-modifier -float 0.5 $?

        // Finder Settings
        $ defaults write com.apple.finder AppleShowAllFiles -bool true $?
        $ defaults write com.apple.finder AppleShowAllExtensions -bool true $?
        $ defaults write com.apple.finder ShowPathbar -bool true $?
        $ defaults write com.apple.finder ShowStatusBar -bool true $?
        $ defaults write com.apple.finder _FXSortFoldersFirst -bool true $?
        $ defaults write com.apple.finder FXPreferredViewStyle -string "Nlsv" $?
        $ defaults write com.apple.finder QuitMenuItem -bool true $?

        // Trackpad Settings
        $ defaults write com.apple.driver.AppleBluetoothMultitouch.trackpad Clicking -bool true $?
        $ defaults write com.apple.AppleMultitouchTrackpad Clicking -bool true $?
    }
}

// Stage 1: macOS Environment Setup
// Ensure the macOS environment is prepared for installation
stage_header("macOS Environment Setup", STAGE)
{
    // Verify Xcode Command Line Tools installation
    stage_step("Checking for Xcode CLI tools installation")
    if not is_command("xcode-select") {
        step_update("Installing Xcode CLI tools")
        $ xcode-select --install $ failed(code) {
            log_message("Xcode CLI tools installation failed with code {code}")
            step_failed("Failed to install Xcode CLI tools")
        }
        
        let message = "Xcode CLI tools installed successfully"
        step_success(message)
        log_message(message)
    } else {
        let message = "Xcode CLI tools are already installed"
        step_success(message)
        log_message(message)
    }

    stage_spacer()

    // Verify Homebrew installation
    stage_step("Checking for Homebrew installation")
    if not is_command("brew") {
        step_update("Installing Homebrew")
        $ /bin/bash -c "\$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"$ failed(code) {
            let message = "Failed to install Homebrew \{{code}\}"
            log_message(message)
            step_failed(message)
        }
        let message = "Homebrew installed successfully"
        step_success(message)
        log_message(message)
    } else {
        step_update("Homebrew detected, performing update")
        silent $ brew update $ failed(code) {
            let message = "Failed to update Homebrew \{{code}\}"
            log_message(message)
            step_failed(message)
        }
        step_update("Upgrading existing Homebrew packages")
        silent $ brew upgrade $ failed(code) {
            let message = "Failed to upgrade Homebrew packages \{{code}\}"
            log_message(message)
            step_failed(message)
        }
        let message = "Homebrew is up to date"
        step_success(message)
        log_message(message)
    }
}
stage_footer(STAGE, "macOS Environment Setup")

// Stage 2: Homebrew Package Installation
// Install necessary packages via Homebrew using a Brewfile
stage_header("Installing Homebrew packages", STAGE)
{
    stage_step("Verifying Brewfile existence")
    if not file_exists("./Brewfile") {
        let message = "Brewfile not found in current directory; Attempting download from repository..."
        log_message(message)
        step_update(message)

        file_download("https://raw.githubusercontent.com/jpdck/capsuleOS/main/Brewfile", "./Brewfile") failed(code) {
            let message = "Failed to download Brewfile \{{code}\}"
            log_message(message)
            step_failed(message)
        }
    }

    step_update("Installing Homebrew packages from Brewfile")
    $ brew bundle install $ failed(code) {
        log_message("Failed to install Homebrew packages via Brewfile \{{code}\}")
        step_failed("Failed to install Homebrew packages via Brewfile")
    }
    let message = "Homebrew packages installed successfully from Brewfile"
    step_success(message)
    log_message(message)
}
stage_footer(STAGE, "Installing Homebrew packages")

// Stage 3: 1Password Integration Setup
// Configure 1Password CLI and import necessary credentials
stage_header("Setting up 1Password integration", STAGE)
{
//     stage_step("Retrieving 1Password Service Token")
//     {
//         let op_token = env_var_get("OP_SERVICE_ACCOUNT_TOKEN") failed {
//             log_message("1Password Service Token not found in environment variables")
//             step_update("Token not in environment variables")
//             input_prompt("│  Please enter your 1Password Service Token: ")
//         }

//         //sanitize input

//         silent $ printf '%s' "{op_token}" | security add-generic-password -a "{USER}" -s "op-service-token" -w -U $ failed(code) {
//             let message = "Failed to store token in Keychain, you may need to add it manually"
//             step_warning(message)
//             log_message(message + ": {code}")
//         }
//     }

//     stage_step("Initializing 1Password Vault")
//     {
//         let vault_list = silent $ op vault list --format=json | jq -r '.[].name' $ failed(code) {
//             log_message("Failed to list 1Password vaults: {code}")
//             step_failed("Failed to list 1Password vaults")
//         }

//         if not text_contains(vault_list, "ZettoSenshi") {
//             step_failed("Vault not found in 1Password account")
//         } else {
//             step_success("ZettoSenshi vault found")
//         }
//     }

//     stage_step("Importing SSH keys from ZettoSenshi vault")
//     {
//         let ssh_keys = $ op item list --vault="ZettoSenshi" --format=json | jq -r '.[] | select(.category=="SSH_KEY") | .title' $ failed(code) {
//             log_message("Warning: Failed to list SSH keys: {code}")
//             step_failed("Failed to list SSH keys")
//         }
        
//         let ssh_keys_array = split_lines(ssh_keys)

//         if len(ssh_keys_array) == 0 {
//             step_warning("No SSH keys found in ZettoSenshi vault")
//             log_message("No SSH keys found in vault")
//         } else {
//             echo "│      Found {len(ssh_keys_array)} SSH key(s):"
//             for index, key in ssh_keys_array {
//                 echo "│        [{index + 1}] {key}"
//             }
//             log_message("Found {len(ssh_keys_array)} SSH keys in vault")
//         }

//         let ssh_selection = ""

//         if len(trim(ssh_keys)) > 0 {
//             trust $ echo "{ssh_keys}" | nl -w2 -s'. ' $
//         } else {
//             echo "Warning: No SSH keys found in ZettoSenshi vault"
//             ssh_selection = "none"
//         }

//     }


}

stage_footer(STAGE, "Setting up 1Password integration")

// Stage 4: macOS Defaults Configuration
// Apply recommended macOS system settings for optimal performance
stage_header("Configuring macOS settings", STAGE)
{
    if input_confirm("Apply recommended macOS system settings?", true) {
        stage_step("Applying macOS system settings...")

        step_update("Configuring interface, dock, finder, and trackpad settings")
        apply_macos_defaults() failed(code) {
            log_message("Failed to apply macOS defaults", code)
            step_failed("Failed to apply macOS defaults")
        }
        
        // Restarting services
        step_update("Restarting affected services")
        silent $ killall Dock Finder $ failed {
             // Ignore if they aren't running or fail to kill
        }

        step_success("macOS system settings applied")
        log_message("macOS system settings applied")
    } else {
        step_warning("Skipping macOS system settings")
        log_message("Skipped macOS system settings")
    }
}
stage_footer(STAGE, "Configuring macOS settings")

// Stage 5: capsuleOS Repository Setup
// Clone or update the capsuleOS repository for further development
stage_header("Setting up capsuleOS repository", STAGE)
{
    if not dir_exists(REPO_DIR) {
        stage_step("Cloning capsuleOS repository")
        silent $ git clone https://github.com/jpdck/capsuleOS.git "{REPO_DIR}" $ failed(code) {
            let message = "Failed to clone capsuleOS repository"
            log_message(message, code)
            step_failed(message)
        }
    } else {
        stage_step("Updating capsuleOS repository")
        silent trust $ cd "{REPO_DIR}" $
        silent $ git pull origin main $ failed(code) {
            let message = "Failed to update capsuleOS repository"
            log_message(message, code)
            step_failed(message)
        }
    }
}
stage_footer(STAGE, "Setting up capsuleOS repository")

// Stage 6: Dotfiles Configuration
// Set up user dotfiles for personalized shell environment
stage_header("Configuring user dotfiles", STAGE)
{
    stage_step("Verifying GNU Stow installation")
    if not is_command("stow") {
        log_message("GNU Stow not found for dotfiles configuration")
        step_failed("GNU Stow is not installed; cannot configure dotfiles")
    }
    step_success("GNU Stow is installed")
    log_message("GNU Stow found")

    stage_spacer()

    stage_step("Applying dotfiles with GNU Stow")
    const DOTFILES_DIR = "{REPO_DIR}/dotfiles"
    
    // Verify dotfiles directory exists (should exist since we cloned capsuleOS repo in Stage 5)
    if not dir_exists(DOTFILES_DIR) {
        let message = "Dotfiles directory not found in capsuleOS repository"
        log_message(message)
        step_failed(message)
    }
    
    cd DOTFILES_DIR
    
    // Get list of packages (directories) to stow
    let packages_raw = trust $ ls -d */ | sed 's#/##' $
    let packages = split_lines(trim(packages_raw))
    
    step_update("Found {len(packages)} package(s) to stow")
    log_message("Packages to stow: {join(packages, ", ")}")
    
    // Stow each package
    for pkg in packages {
        step_update("Stowing {pkg}...")
        
        // First attempt to stow
        $ stow -t {HOME} {pkg} $ failed(code) {
            // If it failed, remove conflicts and try again
            step_update("Removing conflicts for {pkg}...")
            
            // Get list of files that would be stowed
            let files_to_stow = trust $ find {DOTFILES_DIR}/{pkg} -type f -o -type l $
            let file_list = split_lines(trim(files_to_stow))
            
            // Remove each conflicting file from HOME
            for file_path in file_list {
                // Strip the dotfiles directory prefix using bash parameter expansion
                let prefix = "{DOTFILES_DIR}/{pkg}/"
                let relative_path = trust $ echo "{file_path}" | sed "s|{DOTFILES_DIR}/{pkg}/||" $
                let target_file = "{HOME}/{trim(relative_path)}"
                
                if file_exists(target_file) {
                    silent trust $ rm -f "{target_file}" $
                    log_message("Removed conflicting file: {target_file}")
                }
            }
            
            // Try stowing again
            $ stow -t {HOME} {pkg} $ failed(retry_code) {
                let message = "Failed to stow {pkg} after removing conflicts"
                log_message(message, retry_code)
                step_failed(message)
            }
        }
        
        step_success("Successfully stowed {pkg}")
        log_message("Successfully stowed {pkg}")
    }
    
    step_success("Dotfiles configuration complete")
    log_message("All dotfiles packages processed")
}
stage_footer(STAGE, "Configuring user dotfiles")