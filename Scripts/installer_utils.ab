import { echo_error, echo_colored } from "std/env"
import { lpad, rpad, trim } from "std/text"
import { date_now, date_format_posix } from "std/date"
import { file_append, file_read, file_exists } from "std/fs"

const LOG_FILE = "installer.log"

// Get script directory to read VERSION file from correct location
const SCRIPT_DIR = trust $ cd "\$(dirname "\$0")" && pwd $

pub fun title_header(): Null {
    let border = "═══════════════════════════════════════════"
    let width = len(border) + 2
    
    echo ""
    echo "╔{border}╗"
    let header = "capsuleOS AMBER INSTALL SYSTEM"
    let header_padding = (width - len(header)) / 2
    let header_line = rpad(lpad(header, " ", header_padding + len(header)), " ", width-2)
    echo "║{header_line}║"
    echo "╚{border}╝"
    echo ""
}

pub fun title_footer(): Null {
    let border = "═══════════════════════════════════════════"
    let width = len(border) + 2
    
    echo ""
    echo "╔{border}╗"
    let header = "capsuleOS AMBER INSTALL SYSTEM"
    let header_padding = (width - len(header)) / 2
    let header_line = rpad(lpad(header, " ", header_padding + len(header)), " ", width-2)
    echo "║{header_line}║"

    let footer = "INSTALLATION COMPLETE"
    let footer_padding = (width - len(footer)) / 2
    let footer_line = rpad(lpad(footer, " ", footer_padding + len(footer)), " ", width-6)
    echo "║> {footer_line} <║"
    echo "╚{border}╝"
    echo ""
}

pub fun stage_header(stage_name: Text, ref stage: Int): Null {
    stage = stage + 1
    silent trust $sleep 1$
    echo ""
    echo_colored("═══ STAGE {stage}: {stage_name} ═══", 36)
    echo ""
}

pub fun info(message: Text): Null {
    echo_colored("  {message}", 37)
}

pub fun warning(message: Text): Null {
    echo_colored("  [WARNING] {message}", 33)
}

pub fun error(message: Text): Null {
    echo_colored("  [ERROR] {message}", 31)
}

pub fun log_message(message: Text, failure_code: Int = 0): Null {
    let timestamp = date_format_posix(date_now()) failed {
        "UNKNOWN"
    }

    let formatted_message = failure_code != 0
        then "{message} (Error Code: {failure_code})"
        else message

    file_append(LOG_FILE, "[{timestamp}] {formatted_message}\n") failed {
        echo_error("Failed to write to log file: {LOG_FILE}")
    }
}

pub fun fatal_error(message: Text, code: Int = 1): Null {
    log_message(message, code)
    error(message)
    exit code
}

pub fun log_info(message: Text): Null {
    info(message)
    log_message(message)
}

pub fun is_empty(text: Text): Bool {
    return len(trim(text)) == 0
}

pub fun configure_brew_path(): Null {
    if {
        file_exists("/opt/homebrew/bin/brew"): {
            silent trust $ eval "\$(/opt/homebrew/bin/brew shellenv)" $
        }
        file_exists("/usr/local/bin/brew"): {
            silent trust $ eval "\$(/usr/local/bin/brew shellenv)" $
        }
    }
}